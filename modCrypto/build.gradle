ext.moduleName = 'aion.crypto'

// set the publish to true when the code ready to push the lib to the maven repo
def publish = false;

apply plugin: 'maven'
apply plugin: 'signing'

group = "network.aion"
archivesBaseName = "crypto4j"

def getCommitHash = { ->
    def hashStdOut = new ByteArrayOutputStream()
    exec {
        commandLine "sh", "-c", "git log --pretty=format:%h | head -1"
        standardOutput = hashStdOut
    }

    return hashStdOut.toString().trim()
}

dependencies {
    compile 'network.aion:util4j:0.4.0'
    compile 'network.aion:rlp4j:0.4.0'

    compile 'com.madgag.spongycastle:prov:1.58.0.0'
    compile 'com.madgag.spongycastle:core:1.58.0.0'
    compile 'org.slf4j:slf4j-api:1.7.25'

    testCompile 'com.google.truth:truth:0.42'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile "org.mockito:mockito-core:2.23.0"
}

tasks.withType(JavaCompile) {
   doFirst {
      println "Args for for $name are $options.allCompilerArgs"
   }
}

sourceSets {

    if (publish) {
        version = "0.4.0"
    } else {
        jar.baseName = 'crypto4j-' + getCommitHash()
    }

    main {
        java.srcDirs = ['src/main/java']
    }
    test {
        java.srcDirs = ['src/test/java']
    }
}


task compileNative(type: Exec) { 
    workingDir 'src/main/c'
    commandLine 'make'
}

signing {
    sign configurations.archives
}
signArchives.enabled = publish

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
sourcesJar.enabled = publish


javadoc {
  inputs.property("moduleName", moduleName)
  doFirst {
    options.addStringOption('-module-path', classpath.asPath)
    options.tags = [ "implNote" ]
  }
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
javadocJar.enabled = publish


artifacts {
    archives sourcesJar, javadocJar
}

uploadArchives {
  repositories {
    mavenDeployer {

      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'crypto4j'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'a crypto module for the aion java kernel.'
        url 'https://github.com/aionnetwork/aion/tree/master-pre-merge/modCrypto'

        scm {
          connection 'scm:git:https://github.com/aionnetwork/aion.git'
          developerConnection 'git:https://github.com/aionnetwork/aion.git'
          url 'https://github.com/aionnetwork/aion/tree/master'
        }

        licenses {
          license {
            name 'MIT'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'aion foundation dev'
            name 'aion foundation dev'
            email 'toengineering@aion.network'
          }
        }
      }
    }
  }
}
uploadArchives.enabled = publish

build.dependsOn compileNative 
test.dependsOn copyNativeLibsForModuleTests
clean.dependsOn deleteNativeLibs

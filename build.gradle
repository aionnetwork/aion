plugins {
    id "de.undercouch.download" version "3.4.3"
}

allprojects { 
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        jcenter()

        flatDir {
            dirs './lib'
        }
    }
}

ant.importBuild('build.xml') { antTargetName -> 'ant-' + antTargetName }

// referencing the ant values for now; once everything is working,
// use the constants instead
def dirBuildFile = ant.properties['dir.buildfile'] // = "./build/main"
def dirBuildNative = ant.properties['dir.build.native'] // = "./build/main/native"
def dirNative = ant.properties['dir.native'] // = "./native"
def dirWorkspace = ant.properties['dir.workspace'] // = "./"
def dirPack = ant.properties['dir.pack'] // = "./pack"
def dirLibFile = ant.properties['dir.libfile'] // = ""
def dirModFile = ant.properties['dir.modfile']  // ="./mod"
def javaHome = "$System.env.JAVA_HOME"
def vmVersion = ant.properties['vm.version'] // = "0.2.1"

def dirRuntimeJars = 'jars' // we'll store mod and lib stuff in here

task compileNative(type:Exec) {
    // seems kinda hacky, but need big reorganization to fix
    dependsOn 'modCrypto:compileNative'
    dependsOn 'modAionImpl:compileJava'
    //dependsOn 'aion_api:compileJava'

    doFirst { 
        mkdir "${dirBuildFile}"
        mkdir "${dirNative}/linux/equihash"
        mkdir "${dirNative}/linux/equihash"
        mkdir "${dirBuildNative}/linux/equihash"
        ant.copy(includeemptydirs: "false", todir: "./build/native") {
            fileset(dir: "./modAionImpl/src/org/aion/equihash/native")
        }
        ant.copy(includeemptydirs: "false", todir: "./build/native") {
            fileset(dir: "./modAionImpl/build/native")
        }
    }
    commandLine "g++", 
        "-fPIC", 
        "-shared", 
        "-I${javaHome}/include", 
        "-I${javaHome}/include/linux", 
        "-I${dirBuildNative}", 
        "-I${dirNative}/linux/sodium", 
        "-mtune=generic", 
        "-m64", 
        "-std=c++11", 
        "-Wall", 
        "-Wno-deprecated-declarations", 
        "-D_POSIX_C_SOURCE=200112L", 
        "-O3", 
        "./build/native/equi.h", 
        "./build/native/equi_miner.h", 
        "./build/native/equiMiner.cpp", 
        "-L${dirWorkspace}/native/linux/sodium", 
        "-lsodium", 
        "-o", 
        "${dirNative}/linux/equihash/equiMiner.so", 
        "-Wl,-rpath,${dirNative}/linux/sodium"
    def stdout = new ByteArrayOutputStream()
    ext.output = {
        return stdout.toString()
    }
}

subprojects {
    sourceSets { 
        main {
            java { srcDirs = ['src'] }
        }
        test {
            java { srcDirs = ['test'] }
        }
    }

    task copyJarToAionMod(type: Copy) { 
        task -> from "build/libs/${task.project.name}.jar" into file("../mod")
    }

    task deleteJarFromAionMod(type: Delete) { 
        task -> delete "../mod/${task.project.name}.jar"
    }

    task copyNativeLibsForModuleTests(type: Copy) { 
        dependsOn rootProject.compileNative
        from rootProject.file('native') into file('./native')
    }

    task deleteNativeLibs(type: Delete) {
        task -> delete 'native'
    }

    build.dependsOn copyJarToAionMod
    clean.dependsOn deleteJarFromAionMod

    test { 
        reports { 
            html.enabled = false;
        }
        //testResultsDirName = "${rootProject.projectDir}/reports"
    }

    afterEvaluate { 
        // Override java plug-in behaviour to make JDK9+ module logic work
        // From: https://guides.gradle.org/building-java-9-modules/#step_2_produce_java_9_modules_for_all_subprojects
        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                    '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }
        
        // Need to comment this out for now, modules-info.java doesn't have 'require' declarations
        // used by the test code.  The ant build.xml, similarly, uses module-path for build, but 
        // classpath for building the tests (at least in modRlp).  Need to sort out how to properly set the module-path
        // for the tests before using the stuff below.
        /*
        compileTestJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    '--add-modules', 'junit',
                    '--add-reads', "$moduleName=junit",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                ]
                classpath = files()
            }
        }

        test {
            inputs.property("moduleName", moduleName)
            doFirst {
                jvmArgs = [
                    '--module-path', classpath.asPath,
                    '--add-modules', 'ALL-MODULE-PATH',
                    '--add-reads', "$moduleName=junit",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                ]
                classpath = files()
            }
        }
        */
    }
}

task downloadVmDepsGit(type: Download) { 
    overwrite false // TODO This seems to work only when this task is directly invoked
    src([
        "https://github.com/aionnetwork/aion_fastvm/releases/download/v${vmVersion}/fastvm_v${vmVersion}.tar.gz",
        "https://github.com/aionnetwork/aion_fastvm/releases/download/v${vmVersion}/solidity_v${vmVersion}.tar.gz"
    ])
    dest 'build'
}

task extractVmDepsGit(dependsOn: downloadVmDepsGit, type: Copy) { 
    from tarTree("build/fastvm_v${vmVersion}.tar.gz") 
    from tarTree("build/solidity_v${vmVersion}.tar.gz") 
    into "${dirNative}/linux"
    doLast {
        ant.move file:  "${dirNative}/linux/fastvm_v${vmVersion}",
                 tofile: "${dirNative}/linux/fastvm"
        ant.move file:  "${dirNative}/linux/solidity_v${vmVersion}",
                 tofile: "${dirNative}/linux/solidity"
    }
}

task collectDependentJars(type: Copy) { 
    into dirRuntimeJars
    from { subprojects.configurations.runtime } 
    from { subprojects.jar}
    from { file("lib/libminiupnpc.so") } // called by a jar that expects this to be in same dir
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task prePack(type:Exec)  { 
    dependsOn ':aion_api:build'
    commandLine 'sh', 'script/prepack.sh' 
}
task postPack(type:Exec) { commandLine 'sh', 'script/postpack.sh' }

/** Replaces `ant pack_build` */
task pack(type: Tar) { 
    archiveName = 'aion.tar.bz2'
    destinationDir = file(dirPack)
    compression = Compression.BZIP2

    into('/aion/jars') { 
        from dirRuntimeJars 
        include '*.jar', '*.so'
    }
    into('/aion/') { 
        from dirWorkspace
        include 'aion.sh', 'aion_gui.sh'
    }
    into('/aion/native') {
        from dirNative
        include '**'
    }
    into('/aion/config') {
        from "${dirPack}/config"
        include '**'
    }
    into('/aion/rt') {
        from "${dirPack}/rt"
        include '**'
    }
    into('/aion/web3') {
        from "${dirPack}/web3"
        include '**'
    }
    into('/aion/docs') {
        from "${dirPack}/docs"
        include '**'
    }
    into('/aion/clientAPI') {
        from "${dirPack}/clientAPI"
        include '**'
    }
    into('/aion/script') {
        from "${dirPack}/script"
        include 'generateSslCert.sh', 'nohup_wrapper.sh'
    }
}

pack.dependsOn build
pack.dependsOn prePack
pack.dependsOn collectDependentJars 
pack.finalizedBy(postPack)

task packDocker(type: Exec) { 
    dependsOn pack
    commandLine 'sh', "${dirWorkspace}/script/pack_docker.sh"
}

task packDevDocker(type: Exec) { 
    commandLine 'sh', "${dirWorkspace}/script/pack_dev_docker.sh"
}

clean { 
    delete 'pack'
}

/*
task aggregatedReport(type: TestReport) { 
    reportOn subprojects*.test
    destinationDir = file("${buildDir}/reports")
}
*/

build.dependsOn("ant-3rd_build")
build.dependsOn("extractVmDepsGit")
build.dependsOn("collectDependentJars")


project(':modAionImpl') { build.finalizedBy(compileNative) }

/** Replaces `ant ci_build` */
task ciBuild { 
    dependsOn build
    def ciModules = ['modAionBase', 
        'modCrypto', 
        'modP2pImpl', 
        'modTxPoolImpl', 
        'modRlp', 
        'modDbImpl', 
        'aion_fastvm/modFastVM', 
        'modAionImpl', 
        'modPrecompiled', 
        'modApiServer'] 

    configure(subprojects.findAll { it.name in ciModules }) {
        it.test { testResultsDirName = "${rootProject.projectDir}/report" }
        dependsOn it.test
    } 

}
